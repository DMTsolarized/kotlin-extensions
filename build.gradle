plugins {
    id 'signing'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'org.jetbrains.dokka' version '1.4.0'
}

group 'com.github.inforion'
version '0.3.6'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.4.2'

    implementation group: 'joda-time', name: 'joda-time', version: '2.10.6'
    implementation group: 'io.javalin', name: 'javalin', version: '3.12.0'
    implementation group: 'com.konghq', name: 'unirest-java', version: '3.9.00'

    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.11.3'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.12', version: '2.11.3'

    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'

    implementation group: 'com.github.inforion', name: 'kotlin-logging', version: '0.2.4'

    implementation group: 'org.jblas', name: 'jblas', version: '1.2.4'

    implementation group: 'org.jetbrains.kotlinx.spark', name: 'core-3.0_2.12', version: '1.0.0-preview2'
    implementation group: 'org.jetbrains.kotlinx.spark', name: 'kotlin-spark-api-3.0', version: '1.0.0-preview2'
    implementation group: 'org.jetbrains.kotlinx.spark', name: 'kotlin-spark-api-common', version: '1.0.0-preview2'
    implementation group: 'org.jetbrains.kotlinx.spark', name: 'kotlin-spark-api-parent_2.12', version: '1.0.0-preview2'

    implementation group: 'org.apache.spark', name: 'spark-sql_2.12', version: '3.1.1'
    implementation group: 'org.apache.spark', name: 'spark-core_2.12', version: '3.1.1'
    implementation group: 'org.apache.spark', name: 'spark-streaming_2.12', version: '3.1.1'
    implementation group: 'org.apache.spark', name: 'spark-streaming-kafka-0-10_2.12', version: '3.1.1'
    implementation group: 'org.apache.spark', name: 'spark-sql-kafka-0-10_2.12', version: '3.1.1'

    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.7.0'

    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.2'

    implementation group: 'org.jline', name: 'jline', version: '3.15.0'

    implementation group: 'org.mongodb', name: 'mongo-java-driver', version: '3.12.8'
    implementation group: 'org.mongodb', name: 'mongodb-driver', version: '3.12.7'
    implementation group: 'org.mongodb', name: 'bson', version: '4.2.2'

    implementation group: 'org.python', name: 'jython-standalone', version: '2.7.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        // remove when kotlin 1.5.0 will be released
        // see https://youtrack.jetbrains.com/issue/KT-46562
        freeCompilerArgs += '-Xsam-conversions=class'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        // remove when kotlin 1.5.0 will be released
        // see https://youtrack.jetbrains.com/issue/KT-46562
        freeCompilerArgs += '-Xsam-conversions=class'
    }
}

dokkaJavadoc.configure {
    dokkaSourceSets {
        configureEach {
            includeNonPublic = false
            reportUndocumented = true
            jdkVersion = "11"
            skipEmptyPackages = true
            noStdlibLink = false
            noJdkLink = false
        }
    }
}

task dokkaJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            credentials {
                username = project.properties["mavenUsername"]
                password = project.properties["mavenPassword"]
            }

            def releasesRepoUrl = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsRepoUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }

    publications {
        kotlinExtensions(MavenPublication) {
            from components.kotlin

            artifact tasks.dokkaJar
            artifact tasks.sourcesJar

            pom {
                groupId = group
                artifactId = "kotlin-extensions"
                name = "kotlin-extensions"
                version = version
                url = "https://github.com/inforion/kotlin-extensions"
                description = 'Kotlin extension methods and function for different Java libraries'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name = 'Alexei Gladkikh'
                        email = 'gladkikhalexei@gmail.com'
                        organization = 'INFORION, LLC'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/inforion/kotlin-extensions.git'
                    developerConnection = 'scm:git:ssh://github.com/inforion/kotlin-extensions.git'
                    url = "https://github.com/inforion/kotlin-extensions"
                }
            }
        }
    }
}

signing {
    required = project.hasProperty('signing.gnupg.keyName')
    if (required) useGpgCmd()
    sign publishing.publications.kotlinExtensions
}